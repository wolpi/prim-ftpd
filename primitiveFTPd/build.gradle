buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1.7'
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'maven-publish'
//apply plugin: 'android-cq'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "org.primftpd"
        minSdkVersion 15
        targetSdkVersion 33
        versionCode 66
        versionName "7.3-SNAPSHOT"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        }
    }

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }

            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
        }
    }

    packagingOptions {
        // required due to jars
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/BCKEY.SF'
        exclude 'META-INF/BCKEY.DSA'
    }

    lintOptions {
        // we cannot be sure that all translations are present
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    implementation.exclude group: 'org.slf4j', module: 'slf4j-api'
    implementation.exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    implementation.exclude group: 'org.springframework'
}

dependencies {
    // print dependency tree:
    // ./gradlew primitiveFTPd:dependencies --configuration releaseCompileClasspath
    implementation project(':pftpd-pojo-lib')
    implementation project(':slf4j-android-1.6.1-RC1')
    implementation project(':apache-ftpserver-1.1.1-core')
    implementation project(':apache-ftpserver-1.1.1-ftplet-api')
    implementation project(':sshd-core-0.14.0')
    implementation 'org.apache.mina:mina-core:2.1.6'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.fragment:fragment:1.5.7'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.0'
    implementation ('me.dm7.barcodescanner:zxing:1.9.8') {
        exclude group: 'com.android.support'
    }

    implementation 'org.bouncycastle:bcpg-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.primftpd'
            artifactId = 'app'
            version = 'latest-SNAPSHOT'

            artifact file('build/outputs/apk/debug/primitiveFTPd-debug.apk');
        }
    }
/*
    //to debug publishing
    repositories {
        maven {
            url = "file://$projectDir/deploy"
        }
    }
*/
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/wolpi/prim-ftpd")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME") ?: 'never-mind'
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
